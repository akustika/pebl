#!/usr/local/bin/pebl
define Start(lPar)
{
  gBG <- MakeColor("GREY")
  gFG <- MakeColor("RED")
  black <- MakeColor("black")
  gFont <- MakeFont("Vera.ttf",0,44,gFG,gBG,1)
  font2 <- MakeFont("Vera.ttf",0,15,black,gBG,1)
  gFixation <- MakeLabel("+",gFont)
  lStimuli <- Shuffle(RepeatList(["a", "d", "c", "e", "d", "o", "n"],5))

  
  gWin <- MakeWindow()

  AddObject(gFixation,gWin)	
  Move(gFixation, 300,200)
  Hide(gFixation)

  feedback <- MakeLabel("",font2)
  AddObject(feedback,gWin)
  Move(feedback, 300,400)

  inst <- EasyTextBox("Simple reaction time task. Each trial will begin with a fixation cross.  Shortly afterward, letter will appear. Hit any key on the keyboard as quickly as possible after the letter appears. Press any key to begin",100,100,gWin,16,500,200)

   
  Draw()	  
  WaitForAnyKeyPress()
  RemoveObject(inst,gWin)
  Draw()
  Wait(1000)

  sum <- 0
  n <- 0
  min <- 9999999
  loop(i, lStimuli)
  {
    Wait(200)
    rt <- Trial(i) 
    sum <- sum + rt
    n <- n + 1
    if(rt < min)
    {
      min <- rt
    }

    Print(i + "   " + rt)
    
    feedback.text <- "RT: [" + rt + "] mean: [" + Round(sum/n)  + "] Min: [" +min + "]"

  }

  gFixation.text <- "Press any key to quit."
  Show(gFixation)
  Draw()
  WaitForAnyKeyPress()
}


define Trial(stim)
{

  ##Make fixation and stimulus
  lText <- MakeLabel(stim, gFont)

  ##Add labels to window
  AddObject(lText,gWin)	


  ##Move them to the same place

  Move(lText, 300,200)

  ##Hide the text; show fixation
  Show(gFixation) 
  Hide(lText)
  Draw(gWin)

  #hide the fixation after 200-400 ms

  Wait(1200 + RandomDiscrete(3000))


  #Show the text after 100-200 ms
  Show(lText)
  Hide(gFixation)
  Wait(100 + RandomDiscrete(100))
  Draw()
  lTime1 <- GetTime()
  Hide(lText)
   WaitForAnyKeyPress()
  lTime2 <- GetTime()
  Draw()
  RemoveObject(lText, gWin)

  return lTime2 - lTime1	
}


