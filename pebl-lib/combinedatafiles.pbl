## This function will search for and combine data files within 
## the directory you are running in.
##

define Start(p)
{
  gVideoWidth <- 800
  gVideoHeight <- 600
  gSleepEasy <- 1
  gWin <- MakeWindow("grey90")

  directory <- "."
  filter <- "*"
  exclude <- ""
  filename <- "pooled.csv"

  fontsize <- 12
  gSelected <- 1

  head <- EasyLabel("PEBL Data File Merge", gVideoWidth/2,20,gWin,30)


  helpdefault <- "Instructions:  
*Specify matching and exclusion filters to add or remove filenames from the merge.
*Check 'Add filename to data' to put the filename on each line.
*Check 'Files contain header' if your data files contain a common header line.  This will strip the header from each file and add a single header to the merged file.
* Matching filters are applied first, followed by exclusion filters.  Within filter, space or | indicates OR, & indicates AND. ANDs are handled before OR.
* Example: add [.csv dat] to include all files having either .csv and dat in their names. Add [.csv&test] to include only files that have both .csv and test in their names. Add [pooled] to excludes to exclude all files with pooled in their names."

  help <- EasyTextBox(helpdefault,10,270,gWin,12,260,310)

  gFileWindow <- EasyTextBox("",300,50,gWin,fontsize,450,400)
  gFileWindow.linewrap <-0

  windowhead <- Easylabel("Files identified:",gfileWindow.x, gfilewindow.y-8,gWin,fontsize)
  gyHeight <- windowhead.height 
  windowhead.x <- gFileWindow.x+windowhead.width/2
  gfilepreview <- EasyTextBox("",300,470,gWin,fontsize,450,100)
  gfilepreview.linewrap <- 0
  gPreviewHead <- Easylabel("Preview:",gfilepreview.x, gfilePreview.y-8,gWin,fontsize)
  gPreviewHead.x <- gFilePreview.x + gPreviewHead.width/2
  gSelection <- Rectangle(gfileWindow.x+gfileWindow.width/2,0,
                      gfileWindow.width,gyheight,MakeColor("black"),0)

  AddObject(gSelection,gWin)

  helpfont <- MakeFont(gPeblBaseFont,0,15,MakeColor("blue"),MakeColor("black"),0)

  cb1 <- MakeCheckBox("Add filename to data",40,100,gWin,200)
  cb2 <- MakeCheckBox("Files contain header",40,125,gWin,200)

  filterBox <- EasyTextbox(filter,100,150,gWin,12,190,30)
  excludeBox <- EasyTextbox(exclude,100,190,gWin,12,190,30)
  filenamebox <-EasyTextbox(filename,100,230,gWin,12,190,15)

  filtername <- EasyLabel("Match:",0,0,gWin,12)
  Move(filtername, filterbox.x-filtername.width/2-5,
                   filterbox.y+filterbox.height/2)

  excludename <- EasyLabel("Excludes:",0,0,gWin,12)
  Move(excludename, excludebox.x-excludename.width/2-5,
                   excludebox.y+excludebox.height/2)


  filenamename <- EasyLabel("Save file:",0,0,gWin,12)
  Move(filenamename, filenamebox.x-filenamename.width/2-5,
                   filenamebox.y+filenamebox.height/2)


  preview <- MakeButton(140,35,200,gWin,"Combine and open")
  combine   <- MakeButton(140,55,200,gWin,"Combine and save")
  exit      <- MakeButton(140,75,200,gWin,"Exit")


  h1 <- MakeLabel("?",helpfont);Addobject(h1,gWin);Move(h1,preview.x-preview.width/2-5,preview.y)
  h2 <- MakeLabel("?",helpfont);Addobject(h2,gWin);Move(h2,combine.x-combine.width/2-5,combine.y)
  h3 <- MakeLabel("?",helpfont);Addobject(h3,gWin);Move(h3,cb1.x-cb1.width/2-5,cb1.y)
  h4 <- MakeLabel("?",helpfont);Addobject(h4,gWin);Move(h4,cb2.x-cb2.width/2-5,cb2.y)
  h5 <- MakeLabel("?",helpfont);Addobject(h5,gWin);Move(h5,filtername.x-filtername.width/2-5,filtername.y)
  h6 <- MakeLabel("?",helpfont);Addobject(h6,gWin);Move(h6,excludename.x-excludename.width/2-5,excludename.y)
  h7 <- MakeLabel("?",helpfont);Addobject(h7,gWin);Move(h7,filenamename.x-filenamename.width/2-5,filenamename.y)  
  h8 <- MakeLabel("?",helpfont);Addobject(h8,gWin);Move(h8,windowhead.x-windowhead.width/2-5,windowhead.y)  
  h9 <- MakeLabel("?",helpfont);Addobject(h9,gWin);Move(h9,gPreviewHead.x-gPreviewHead.width/2-5,gPreviewHead.y)  
  h10 <- MakeLabel("?",helpfont);Addobject(h10,gWin);Move(h10,help.x-5,help.y)  


  gfileList <- GetFilteredFiles(directory,filter,exclude)
  gfilewindow.text <- ListToText(gfilelist)
  SetSelected(1)  ##we get a crash here if there is no file in gFileList
  Draw()
   objs <- [cb1,cb2]
   cont <- 1
   while (cont)
   {


     Draw()
     out <- WaitForClickOnTarget([cb1,cb2,preview,filterbox,
                                   excludebox,gfilewindow,filenamebox,
				   combine,exit,
                                     h1,h2,h3,h4,h5,h6,h7,h8,h9,h10],
                                      [1,2,3,4,5,6,7,8,9,
                                       11,12,13,14,15,16,17,18,19,20])
     if(out==1)
     {
        ClickCheckBox(cb1)
        Draw()
     }elseif(out==2)
     {	
        ClickCheckBox(cb2)
        Draw()
     }elseif(out==3)
     {
        PushButton(preview)
        gfileList <- GetFilteredFiles(directory,filter,exclude)
        CombineDataFiles(directory,filename,filter,exclude,cb2.state,cb1.state)
        Launchfile(filename)
        Draw()

     } elseif(out==4)
     {
        ##filter box
	relx <- First(gClick) - (filterBox.x )
	rely <- Second(gClick) - (filterBox.y )

  	filterBox.cursorpos <-  GetTextBoxCursorFromClick(filterBox,relx,rely)
        filter <- GetInput(filterbox,"<return>",1)
	gFileList <- GetFilteredFiles(directory,filter,exclude)
        gfilewindow.text <- ListToText(gFileList)
        SetSelected(1)
        Draw()

     }elseif(out==5)
     {  ##excludebox

	   relx <- First(gClick) - (excludeBox.x )
	   rely <- Second(gClick) - (excludeBox.y )

  	   excludeBox.cursorpos <-  GetTextBoxCursorFromClick(excludeBox,relx,rely)

	   exclude <- GetInput(excludebox,"<return>",1)
	   gfileList <- GetFilteredFiles(directory,filter,exclude)
           gfilewindow.text <- ListToText(gfileList)

	   SetSelected(1)

	   Draw()
     }elseif(out==6)
     {  ##click on file window
        clicky <- Second(gClick)
        SetSelected(Min([Floor((clicky-gfilewindow.y)/gyheight)+1 ,
                         Length(gfilelist)]))
	Draw()

      } elseif(out==7)
      {
          ##filename box
	   relx <- First(gClick) - (filenameBox.x )
	   rely <- Second(gClick) - (filenameBox.y )
  	   filenameBox.cursorpos <-  GetTextBoxCursorFromClick(filenameBox,
                                                            relx,rely)
	   filename <- GetInput(filenamebox,"<return>",1)
	   Draw()
     }elseif(out==8)
     {
     
     PushButton(combine)
     CombineDataFiles(directory,filename,filter,exclude,cb2.state,cb1.state)
     
     }elseif(out==9)
     {
       PushButton(exit)
       cont <- 0
     }elseif(out>=10 and out<=20)
     {
        instructions <- ["This button merges the files and opens them in your default handler (i.e., microsoft excel for csv files)",
                        "This button combines the files and saves the combined file to the save file",
			"Thus button determines whether you strip the first row in each file",
			"This button determines whether you add the filename as a column on each row",
			"Specify match filters here, separated by spaces or | (for OR) or & (for AND).  use * to specify all files. * cannot be combined with other characters, so *.csv does not work, but .csv does.  Examples:
csv files with 'pooled' in their names: csv&pooled
txt files or csv files: txt|csv
",
			"Specify exclusion criteria here, separated by spaces.  Excludes are processed after filters.",
			"Specify file name to save the merged data file as.",
			"This box lists all files matching the constraints. This box does not scroll, so if you match too many files, you will not see the ones at the end.",
       			"This gives you a preview any data file selected in the files window.  This allows you to inspect whether the file should be included, and whether there is a header.",
			helpdefault]
				
        text <- Nth(instructions,out-10)
	help.text <- text
	Draw()
     }
   }   

}


define SplitFilter(filter)
{

  
   filter<- ReplaceChar(filter," ","|")

   ##see if there are any ANDS here
   tmp <- SplitString(filter,"&")


   if(Length(tmp)>1)
   {
     out <- ["AND"]
     loop(i,tmp)
     {
        PushOnEnd(out,SplitFilter(i))
     }
   } else {
      tmp <- SplitString(filter,"|")
      if(Length(tmp)>1)
      {
        out <- ["OR"]
       loop(i,tmp)
       {
         PushOnEnd(out,SplitFilter(i))
       }

      }else{
       out <- First(tmp)       
      }
   }

  return out
}


define MatchToFilter(text,filtertree)
{

  if(IsList(filtertree))
  { 

    if(First(filtertree)=="OR")
    {
       match <- 0
      loop(i,Sequence(2,Length(filtertree),1))
      {
           match <- match OR MatchToFilter(text,Nth(filtertree,i))

      }
    }else{ ##AND
      match <- 1
      loop(i,Sequence(2,Length(filtertree),1))
      {
           match <- match AND MatchToFilter(text, Nth(filtertree,i))
      }

    }
  }else{
    if(filtertree=="")
    {
      match <- 0
    }elseif(filtertree=="*")
    {
      match <- 1
    }else{
  
    ##not a list; final filter leaf.
    match <- (FindInString(text,filtertree,1)>0 ) or (filtertree=="*")
    }
 }


  return match
}


define FilterFileListing(list,matchfilter,excludefilter)
{
  matches <- SplitFilter(matchfilter)
  excludes <- SplitFilter(excludefilter)
  newlist <- []
  loop(item,list)
  {	

    match <- MatchToFilter(item,matches)

    exclude <- MatchToFilter(item,excludes)

    if(match and (not exclude))
    {
      PushOnEnd(newlist,item)
    }
  }

return newlist
}


define GetFilteredFiles(directory,matches,excludes)
{
    list <- GetFileListing(directory)
    out <- FilterFileListing(list,matches,excludes)
    return out
}

##this gets a hierarchical directory.
define GetFileListing(directory)
{

  ##first, get a listing of the files.
  list <- GetDirectoryListing(directory)

  header <- ""
  fileListing <- []

  id <- 1
  while(id <= Length(list))
   {

    i <- Nth(list,id)

    if(not IsDotFile(i))
      {
    
        ##First check if its a directory.
        if(IsDirectory(directory+"/"+i))
         {
           sublist <- GetDirectoryListing(directory+"/"+i)
	   loop(sfile,sublist)	
            {

	      if(not IsdotFile(sfile))	
	      {
                  PushOnEnd(list, directory+"/"+i+"/"+sfile)
	      }
            }
         }else {
           PushOnEnd(fileListing ,i)        
         }
    }

   id <- id + 1
 }
  return fileListing
}

## This will find .csv files and combine them,
## removing the header if necessary.

define CombineDataFiles(directory,mergefilename,filter,
                       exclude,headerMatch,
                       addFileLine)
{

    header <- ""
    list <- GetFilteredFiles(directory,filter,exclude)
    fileLines <- []

    loop(i,list)
       {

              fileIn <- FileReadList(i)

              if(headerMatch)
	      {
               ##We need to handle special-case emptyfiles that have
 	       ##only headers.

                
 	      if(Length(filein)>0)
	         {
		    header <- First(filein)
  	         }

	      if(Length(filein)>1)
   	         {
             	    fileIn<-SubList(filein,2,Length(filein))
		 }else{
		    ##We have a header-only file.
		    filein <- []
		 }
              }

              if(addFileLine)
                   {
                        out <- []
                        loop(line,fileIn)
			      {
                                PushOnEnd(out,gQuote+i+gQuote+","+line)
                              }
                        fileIn <- out

                   }

             PushOnEnd(fileLines,fileIn)
        }

 

  fileout <- FileOpenOverwrite(mergefilename)
  if(not header=="") 
     {
     if(addFileLine)
        {
          header <- "FILE,"+header
        }
        FilePrint(fileout,header)
      }


     loop(i,Flatten(fileLines))
      {

        FilePrint(fileout,i)
      }

}

##checks to see if a filename is .. or .
##this is just a helper for CombineDataFiles
define IsDotFile(i)
{
  
  isdot <-   SubString(i,StringLength(i),1) == "."


  if(StringLength(i)>1)
  {
    isdot <- isdot or (SubString(i,StringLength(i)-1,2) == "..")
  }
 return isdot
}


define MakeButtonX(x,y,width,win,label)
{


   col <- MakeColor("grey70")
   button <-MakeCanvas(width,20,col)

   

   font<- MakeFont(gPEBLBaseFont,0,15,MakeColor("black"),MakeColor("white"),0)
   label <- MakeLabel(label,font)

   ##If the label is bigger than the button, shrink it.
   if(label.width > (button.width-6))
    {
	   label.zoomx <- (button.width-6)/(label.width)
    }

   AddObject(label,button)
   Move(label,width/2,9)
   Draw(button)
   
   rect <-Rectangle(Floor(button.width/2),Floor(button.height/2),button.width,button.height,MakeColor("grey25"),0)
   AddObject(rect,button)
   Draw(rect)
   Move(rect,rect.x-1,rect.y-1)
   Draw(rect)
   Move(rect,rect.x-1,rect.y-1)
   Draw(rect)
#   Move(rect,rect.x-1,rect.y-1)
#   Draw(rect)
   RemoveObject(rect,button)

   AddObject(button,win)
   
   Move(button,x,y)
   
   return button
}

define SetSelected(index)
{
   gSelected <- index
   gSelection.y <- gfileWindow.y+gyheight*(gselected-.5)

     
  if((Length(gFileList)>0)  and (gSelected <= Length(gFileList)))
   {
    file <- Nth(gfileList,gSelected)

     if(FileExists(file))
       {
           gfilepreview.text <- FileReadNLines(file,20)

         }else{

           gfilepreview.text <- "File does not exist"

         }


  gPreviewHead.text <- "Preview: "+file
  gPreviewHead.x <- gFilePreview.x + gPreviewHead.width/2
 }

}

##This reads N lines of text
define FileReadNLines(file,n)
{
  filein <- FileopenRead(file)
  out <- ""
  loop(i, Sequence(1,n,1))
  {
    out <- out + FileReadLine(filein) +CR(1)
    if(EndOfFile(filein))
    {
      break
    }
   }
  return out
}

define PushButton(tmp)
{
	     Move(tmp,tmp.x+2,tmp.y+2)
		 Draw()
		 Wait(100)
		 Move(tmp,tmp.x-2,tmp.y-2)
		 Draw()
}


## xy is the center of the checkbox
##
define MakeCheckBox(label,x,y,win,width)
{

   col <- MakeColor("grey80")
   back <-MakeButton(x,y,width,win,label)#Canvas(width,20,col)
  

   font<- MakeFont(gPEBLBaseFont,0,15,MakeColor("black"),MakeColor("white"),0)
#   label <- MakeLabel(label,font)

   ##If the label is bigger than the button, shrink it.
#   if(label.width > (back.width-6))
#    {
#        label.zoomx <- (back.width-18)/(label.width)
#    }

#   AddObject(label,back)
#   Move(label,width/2+5,9)
#   Draw(label)
#   Hide(label)

   check <- EasyTextBox("",4,4,back,10,12,12)
   AddObject(check,back)

   
   rect <-Rectangle(10,10,14,14,MakeColor("grey65"),0)
   AddObject(rect,back)
#   Move(rect,back.width/2,back.height/2)
   Draw(rect)

   AddObject(back,win)
   Move(back,x+back.width/2+5,y+9)
   
   checkbox <- MakeCustomObject("checkbox")

   checkbox.name <- "<CUSTOMOBJECT>"
   checkbox.x <- back.x
   checkbox.y <- back.y
   checkbox.width <- back.width
   checkbox.height <- back.height
   checkbox.back <- back
   checkbox.label <- label
   checkbox.check <- check
   checkbox.state <- 0
   return checkbox
}


define ClickCheckBox(cb)
{

 cb.state <- 1- cb.state
 check <- cb.check
 if(cb.state)
 {
 
   check.text <-" X"
 }else{
   check.text <- ""
 }
 Draw(check)
}

define ListToText(list)
{
 text <- ""
 sep <- ""
 loop(i,list)
 {
    text <- text + sep +i
    sep <- CR(1)
 }
return text
}
