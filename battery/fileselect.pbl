##  This is a PEBL script that acts as a cross-platform 
##  launcher for PEBL.
##  Version 0.1: Initial release, to accompany PEBL 0.12.
##  (c) 2011 Shane T. Mueller, Ph.D. smueller@obereed.net
##

define Start(p)
{



  if(Length(p) >0)
   {
     Print(p)
       gConfigName <- First(p)
	   if(gConfigName==0)
        {
		  gConfigName <- "default"
        }
   }else{
       gConfigName <- "default"
   }

  ## todo: 
  ## concatenate data files
  ## open battery.txt file

  ##This allows you to open up the description file directly.
  gEditDescription <- 1

  systemType <- GetSystemType()
  Print(systemtype)
  if(systemType == "LINUX") 
   {

    gViewerCommand <- "gedit"
    gPEBLName      <- "pebl"
    gOpenManual    <- "acroread /usr/local/share/pebl/doc/PEBLManual0.12.pdf&"
	gOpenURL        <- "firefox"
	gManualLoc      <- "/usr/local/share/pebl/doc/PEBLManual0.12.pdf"

		
   }elseif(systemType=="WINDOWS")   {
     
     gViewerCommand <- "c:\windows\notepad.exe"
     gPEBLName      <- "c:\Progra~1\PEBL\bin\pebl.exe"
     gOpenManual    <- ""
     gManualLoc     <- "C:\PEBL.pdf"
     gOpenURL       <- ""


   }elseif(systemType == "OSX")
   {
     gViewerCommand <- "open"
     gPEBLName      <- "/opt/local/bin/pebl"
     gOpenManual    <- "acroread /opt/local/share/pebl/doc/PEBLManual0.12.pdf"


   }

   gSleepEasy <- 1
   gVideoWidth <- 1000
   gVideoheight <- 700
   win <- MakeWindow()

   ReadConfigFile(gConfigname)

  if(gFullscreen)
    {
      fstext  <- "X"
    } else { 
      fstext <- ""
    }
  if(gNimhdemographics)
    {
	  demotext <- "X"
    }else{
	  demotext <- ""
    }


   ##keep track of the offset/select pairs when you move to new directories.
   selectStack <- []
   selected <-1     #Which item is selected in the fileview
   offset <- 0      #offset in the fileview



   logFile <- FileOpenAppend("PEBLLaunch-log.txt")


   width <- 200
   height <- 500
   gutter <- 10   

   xbase <- 10
   ybase <- 100

   dirchain <- ["."]  ##this is a history of the directories you have 
                      ##navigated to.



   fontsize <- 11
   font <- MakeFont(gPEBLBaseFont,0,fontsize,MakeColor("black"),MakeColor("white"),1)

   filelist <- FilterDir(GetDirectoryListing(DirlistToText(dirchain)),dirchain)
   flatfilelist <- Flatten(filelist)
   
 
 




   run        <- MakeButton(xbase+100,ybase-30,200,win,"Run selected script")
   edit       <- MakeButton(xbase+55,ybase+height+20,120,win,"Open")
   exit       <- MakeButton(xbase+55,ybase+height+50,120,win,"EXIT")
   viewoutput <- MakeButton(xbase+200,ybase+height+20,150,win,"View debug output")
   viewerror  <- MakeButton(xbase+200,ybase+height+50,150,win,"View error output")

   addtochain <- MakeButton(xbase+300,ybase+height+20-100,120,win,"Add to chain")
   clearchain <- MakeButton(xbase+300,ybase+height+50-100,120,win,"Clear chain")
   savechain <- MakeButton(xbase+430,ybase+height+50-100,120,win,"Save chain")
   loadchain <- MakeButton(xbase+430,ybase+height+20-100,120,win,"Load chain")



   ##upper right buttons
   openmanual <- MakeButton(690,50,130,win,"Open Manual")
   about      <- MakeButton(850,50,130,win,"About")
   requestfeature <- MakeButton(690,75,130,win,"Request Feature")
   visitweb <- MakeButton(850,75,130,win,"Visit Website")

   ##################################################
   ##  
   ##  graphical widgets related to the chain launcher.

   ##chain-launcher
   chaintitle <- EasyLabel("Experiment Chain:"+gConfigName,xbase+210+150,290,win,18)
   chainlist <- EasyTextBox("",xbase+210,300,win,fontsize,300,200)
   chainlist.linewrap <- 0
   chainlist.text <- MakeExpChainText(gexpchain)


   launchchain <- MakeButton(chaintitle.x,chaintitle.y-20,200,win,"Launch Chain")
 



   
   bg <- MakeCanvas(200,500,MakeColor("grey20"))
   AddObject(bg,win)
   MoveCorner(bg,xbase,ybase)

   
   path <- EasyLabel(DirListToText(dirchain),(xbase+width/2), ybase-10, win,fontsize)
   yheight <- path.height


   portalnum <- Floor((bg.height-gutter*4)/yheight)
   portalheight <- yheight*portalnum

   portal <- MakeTextBox("",font,width-gutter*3,portalheight)

   portal.linewrap <-0
   AddObject(portal,win)
   Move(portal,xbase+gutter,ybase+gutter*2)


   description <- MakeTextBox("Description: No description found.",font,420,250)
   AddObject(description,win)
   Move(description,570,110)

   title <- EasyLabel("PEBL Launcher for "+ GetPEBLVersion(),gVideoWidth/2,20,win,30)

   screenshotbase <- Rectangle(780,530,420,320,MakeColor("black"),1)
   screenshotdummy <- Rectangle(780,530,400,300,MakeColor("white"),1)
   AddObject(screenshotbase,win)
   AddObject(screenshotdummy,win)
   screenshot <- screenshotdummy

   selection <- Rectangle(portal.x+portal.width/2,0,portal.width,yheight,MakeColor("black"),0)
   
   up <- Polygon(xbase+width-gutter,ybase+gutter,[-5,5,0],[5,5,-5],MakeColor("grey"),1)
   down <- Polygon(xbase+width-gutter,portal.y+portal.height+gutter,[-5,5,0],[-5,-5,5],MakeColor("grey"),1)
   
   upbg <- Square(xbase+width-gutter, ybase+gutter,  10,MakeColor("grey"),0)
   downbg <- Square(xbase+width-gutter, portal.y+portal.height+gutter, 10,MakeColor("grey"),0)


   bug <- Rectangle(xBase+width-gutter,ybase+gutter+20,10,20,MakeColor("grey40"),1)
   slidebin <- Rectangle(xBase+width-gutter,portal.y+portal.height/2,10,portal.height,MakeColor("grey70"),1)


   subjectbox <- EasyTextBox(gsubcode,xbase+width+150,ybase,win,fontsize,60,yheight)
   sublabel   <- EasyLabel("Participant code:", xbase+width+70,subjectbox.y+subjectbox.height/2,win,fontsize)
   MoveCorner(sublabel,subjectbox.x-sublabel.width-10,subjectbox.y)
   subboxhighlight <- Rectangle(subjectbox.x+subjectbox.width/2,subjectbox.y+subjectbox.height/2,subjectbox.width,subjectbox.height,MakeColor("red"),0)
   AddObject(subboxhighlight,win)
   Hide(subboxhighlight)


   expbox <- EasyTextBox(gexperimenter,xbase+width+150,ybase+40,win,fontsize,60,yheight)
   explabel   <- EasyLabel("Experimenter:", expbox.x,expbox.y,win,fontsize)
   MoveCorner(explabel,expbox.x-explabel.width-10,expbox.y)
   expboxhighlight <- Rectangle(expbox.x+expbox.width/2,expbox.y+expbox.height/2,expbox.width,expbox.height,MakeColor("red"),0)
   AddObject(expboxhighlight,win)
   Hide(expboxhighlight)

   langbox <- EasyTextBox(glanguage,xbase+width+150,ybase+80,win,fontsize,60,yheight)
   langlabel   <- EasyLabel("Language:", langbox.x,langbox.y,win,fontsize)
   MoveCorner(langlabel,langbox.x-langlabel.width-10,langbox.y)
   langboxhighlight <- Rectangle(langbox.x+langbox.width/2,langbox.y+langbox.height/2,langbox.width,langbox.height,MakeColor("red"),0)
   AddObject(langboxhighlight,win)
   Hide(langboxhighlight)



   ##Fullscreen checkbox:
   fullscreenbox    <-EasyTextBox(fstext,           xbase+width+10,ybase+120,win,fontsize,yheight,yheight)
   fullscreenlabel  <- EasyLabel("Fullscreen", xbase+width+70,ybase+120+yheight/2,win,fontsize)


   ##Demographics checkbox:
   demobox    <-EasyTextBox(demotext,             xbase+width+130,ybase+120,win,fontsize,yheight,yheight)
   demolabel  <- EasyLabel("Collect demographics", xbase+width+230,ybase+120+yheight/2,win,fontsize)

   PrintProperties(demobox)


   AddObject(slidebin,win) 
   AddObject(upbg,win)
   AddObject(up,win)
   AddObject(downbg,win)
   AddObject(down,win)
   AddObject(bug,win)
   AddObject(selection,win)




##Some state variables are initialized here




   portal.text <- DirToText(First(filelist),Second(filelist),offset,dirchain)

   selection.y <- portal.y+yheight*(selected-.5)
   Draw()
   cont <- 1
   while(cont)
    { 



     selection.y <- portal.y+yheight*(selected-.5-offset)
	 bugy <- (offset / (Length(flatfilelist)-portalnum-1))*(portalHeight-gutter*2)
     portal.text <- DirToText(First(filelist),Second(filelist),offset,dirchain)

     ##find out if there is a text description of the selected text.

	 descname <- DirListToText(dirchain)+Nth(flatfilelist,selected)+".about.txt"
	 path.text <- DirListToText(dirchain)
  	 MoveCorner(path,xbase+2,ybase-18)

	 if(FileExists(descname))
      {

	     text <- FileReadText(descname)
		 description.text <- "Description: " + text 
      } else {
         description.text <- "Description: No description found." 
      }

     screenname <-DirListToText(dirchain)+Nth(flatfilelist,selected)+".png"

	 if(FileExists(screenname))
      {

	     RemoveObject(screenshot,win)
	     screenshot <- MakeImage(screenname)

		 AddObject(screenshot,win)

 		 ##scale to 400x300
		 scalew <- 400/screenshot.width  
		 scaleh <- 300/screenshot.height
		 scale <- Min([scalew,scaleh])



		 screenshot.zoomX <- scale
		 screenshot.zoomY <- scale

		 Move(screenshot,780,530)


		 		 

      } else {
	  
	     RemoveObject(screenshot,win)
		 screenshot <- screenshotdummy
		 AddObject(screenshot,win)


      }
	  

	 #make sure bug fits inside its channel.
	 bug.y <- Max([Min([bugY+ybase+gutter*2,slidebin.y+slidebin.height/2-gutter]),portal.y+gutter])

     Draw()

     tmp <-	 WaitForClickOnTarget([portal,upbg,downbg,exit,slidebin,
						run,subjectbox,fullscreenbox,expbox,langbox,
    				   addtochain,clearchain,launchchain,viewoutput,viewerror,
					   edit,openmanual,requestfeature,visitweb,about,
					   description,demobox,savechain,loadchain],
				   [1,2,3,4,5,
				   6,7,8,9,10,
				   11,12,13,14,15,
				   16,17,18,19,20,
				   21,22,23,24])

	 if(tmp==2)  ## up arrow
      {
	    offset <- Max([offset - 1,0])

        if(selected<=offset)
          {
		    selected <- offset + 1
          }
        if(selected > offset+portalnum)
          {
		    selected <- offset+portalnum
          }

      }elseif(tmp==3) ## down arrow
      {
	    offset <- Min([offset+1, Length(flatfilelist)-portalnum])
		selected <- Max([offset+1,selected])

      }elseif(tmp==1)  #Click was on the portal.
      {

	      ##update selection rectangle here
	      clicky <- Second(gClick)
		  newselected <- Floor((clicky-portal.y)/yheight)+1 + offset

		  ##if we click past the last entry, don't adjust selection
		  if(newselected > Length(flatfilelist))
          {
		      selected <- selected ##do nothing
          }elseif(selected == newselected)
 		   {

     	     if(IsDirectory(DirListTotext(dirchain)+Nth(flatfilelist,selected)))
              {		


               dirchain <- AppendDirlist(dirchain,Nth(flatfilelist,selected))
			   if(Length(dirchain)<(Length(selectstack)+1))
               {	

			      ##we are backing up.
				  last <- Nth(selectstack,Length(selectstack))
				  selected <- 	First(last)
			      offset <- Second(last)


				  ##Remove the last item.
				  if(Length(selectstack)<=1)
                  { 
                    selectStack <- []
                  } else {
    
    			      selectStack <-SubList(selectstack,1,Length(selectstack)-1)
				  }
       		     filelist <- FilterDir(GetDirectoryListing(DirlistToText(dirchain)),dirchain)
         		 flatfilelist <- Flatten(filelist)

 			   }elseif(Length(dirchain)>Length(selectstack))
               {
			     #We opened a new subdirectory.
                 selectStack <- Append(selectstack,[selected, offset])
       		     offset <- 0
			
        		   filelist <- FilterDir(GetDirectoryListing(DirlistToText(dirchain)),dirchain)
         		   flatfilelist <- Flatten(filelist)

     		   selected <- Length(First(filelist)) + 1
      		   if(selected > Length(flatfilelist) or 
    		      selected > portalnum)
                    {
                      selected <- 1 
                    }
               }

              }            
            } else {
		      selected <- newselected
           }
		  
      }elseif(tmp == 5) ##click on the slider bin
      {
	    
		filelen <- Length(flatfilelist)
		if(filelen <= portalnum)
         {
           offset <- 0

         } else {

         clicky  <- Second(gClick)
		 
		 proportion <- ((clicky - portal.y)/portal.height)

		 offset  <- Floor(proportion * (filelen - portalnum))
		 if(selected < offset)
          {
             selected <- offset+1
          }
		  if(selected>offset+portalnum)
		  {
		     selected > offset+portalnum
   		  }

         }
      }elseif(tmp ==4) ##Exit
      {


       PushButton(exit)
       ##Saveconfig(gconfigname)

	     cont <- 0
      }elseif(tmp==6) ##hit 'run'
      {
         PushButton(run)

	     if(IsDirectory(DirListTotext(dirchain)+Nth(flatfilelist,selected)))
         {		


           dirchain <- AppendDirlist(dirchain,Nth(flatfilelist,selected))
		   filelist <- FilterDir(GetDirectoryListing(DirlistToText(dirchain)),dirchain)
		   flatfilelist <- Flatten(filelist)
 		   selected <- Length(First(filelist)) + 1
		   if(selected > Length(flatfilelist) or 
		      selected > portalnum)
            {
              selected <- 1 
            }
		   offset <- 0


         } else {

       	  if(gnimhdemographics)
    	  {
	      	  GetNIMHDemographics(gsubcode,win,"demographics-log.csv")
          }

             RunScript(dirchain,Nth(flatfilelist,selected),gfullscreen,glanguage,logfile,gsubcode,gexperimenter)

          }
      }elseif(tmp==7)  ##Change subject code
      {
	   
       ##subject box
	   Show(subboxhighlight)
	   subjectbox.cursorpos <- StringLength(subjectbox.text)
	   Draw()
	   subcode <- GetInput(subjectbox,"<return>")
	   Hide(subboxhighlight)
	   
      }elseif(tmp==8)
      {
	     gfullscreen <- 1-gfullscreen
		 if(gfullscreen)
		 {
		   fullscreenbox.text <- "X"
         }else{
		   fullscreenbox.text <- ""
         }
      }elseif(tmp==9)  ##Change experimenter code
      {
       ##experimenter box
	   Show(expboxhighlight)
	   expbox.cursorpos <- StringLength(expbox.text)
	   Draw()
	   experimenter <- GetInput(expbox,"<return>")
	   Hide(expboxhighlight)
	   
      }elseif(tmp==10)  ##Change language code
      {
       ##languagebox
	   Show(langboxhighlight)
	   langbox.cursorpos <- StringLength(langbox.text)
	   Draw()
	   language <- GetInput(langbox,"<return>")
	   Hide(langboxhighlight)
	   
      }elseif(tmp==11)  ##Add to chain
      {

	     PushButton(addtochain)

		 fname <- Nth(flatfilelist,selected)
		 if(IsPEBLFile(fname))
         {
		   gexpchain <- Append(gexpchain,[dirchain,fname])
		   chainlist.text <- MakeExpChainText(gexpchain)

         }

	   
      }elseif(tmp==12)  ##clear chain
      {
	    PushButton(clearchain)
        gexpchain <- []        
		chainlist.text <- MakeExpChainText(gexpchain)
	   
      }elseif(tmp==13)  ##launch chain
      {
      PushButton(launchchain)
	  if(gnimhdemographics)
	  {
	  	  GetNIMHDemographics(subcode,win,"demographics-log.csv")
      }
 	  loop(i,gexpchain)
       {
	      dc <- First(i)
		  fname <- Second(i)
          RunScript(dc,fname,gfullscreen,glanguage,logfile,gsubcode,gxperimenter)
       }
   
      }elseif(tmp==14)  ##View debug output
      {
	     PushButton(viewoutput)  
 	     LaunchFile(DirListToText(dirchain)+"stdout.txt")
  
#	     SystemCall(gViewerCommand,  DirListToText(dirchain)+"stdout.txt &")
      
      }elseif(tmp==15)  ##View error output
      {
	     PushButton(viewerror)
         LaunchFile(DirListToText(dirchain)+"stderr.txt")
         #SystemCall(gViewerCommand, DirListToText(dirchain)+"stderr.txt &")
      }elseif(tmp==16)  ##Edit script
      {
		  PushButton(edit)
	      if(IsPEBLFile(Nth(flatfilelist,selected)))
            {
              LaunchFile(DirListToText(dirchain)+Nth(flatfilelist,selected))
            } else {

              LaunchFile(DirListToText(dirchain)+Nth(flatfilelist,selected))
            }
      }elseif(tmp==17)  ##Open Manual
      {
	     PushButton(openmanual)
         #SystemCall(gManualLoc,"")	  
        LaunchFile(gManualLoc)
      }elseif(tmp==18)  ##Request Feature
      {
	     PushButton(requestfeature)
         LaunchFile("https://fundry.com/project/69-pebl")


      }elseif(tmp==19)  ##Visit web
      {
	     PushButton(visitweb)
         #SystemCall("http://pebl.sourceforge.net","")	  
	   Launchfile("http://pebl.sourceforge.net")	  
	   

      }elseif(tmp==20)  ##About
      {
	     PushButton(about)
		 MessageBox("PEBL is a system for creating and running psychology experiments.  It is developed by Shane T Mueller, Ph.D., and includes more than 50 standard psychology lab experiments.  You can use PEBL to create your own tests or modify previous ones FREE OF CHARGE. Find out more at http://pebl.sf.net

This launcher is written in PEBL itself, allowing a fairly uniform launcher on all platforms. Only .pbl  files and directories are shown in the file window--use your file manager to get the data after your experiment is complete. Click on a directory that has been selected to open it.  Add scripts to the experiment chain window to run a sequence of experiment in a row, all using the same subject code.  

 *The file [PEBLLaunch-log.txt] contains a log of every experiment launched from the launcher.
 *Debug output from each script (using the Print() command) is saved in the file [stdout.txt] file in a script's directory.
 *Error and automatic messages from each script are saved in the file [stderr.txt] in a script's directory.",win)

      } elseif(tmp==21) ##edit description
      {
        if(gEditDescription)
         {
           #

	     if(GetSystemType()=="LINUX")
          {
         	SystemCall(gViewerCommand + " " + DirListToText(dirchain)+Nth(flatfilelist,selected)+".about.txt&")
          } else {
            LaunchFile(DirListToText(dirchain)+Nth(flatfilelist,selected)+".about.txt")
          }
         }
      }elseif(tmp==22)
      {
	     gnimhdemographics <- 1-gnimhdemographics
		 if(gnimhdemographics)
		 {
		   demobox.text <- "X"
         }else{
		   demobox.text <- ""
         }

      }elseif(tmp==23)  ##sAVE CHAIN
      {
	     PushButton(savechain)
         Saveconfig(gconfigname)


      }elseif(tmp==24)  ##loadchain
      {

	     PushButton(loadchain)
	     gConfigname <- GetEasyInput("Config name?",win)
		 ReadConfigFile(gConfigName)



		 if(gfullscreen)
		 {
		   fullscreenbox.text <- "X"
         }else{
		   fullscreenbox.text <- ""
         }

		 if(gnimhdemographics)
		 {
		   demobox.text <- "X"
         }else{
		   demobox.text <- ""
        }
        chaintitle.text  <- "Experiment Chain:"+gConfigName
        chainlist.text <- MakeExpChainText(gexpchain)




      }
     Draw()
   }


}

##This is a hard-coded filter;
##it will only return .pbl files and directories.
define FilterDir(inlist,path)
{

  pathhead <- DirListTotext(path)
  tmpdir <- []
  tmppbl <- []
  loop(i, inlist)
  {

    if(IsDirectory(pathhead+i))
      {

       tmpdir <- Append(tmpdir,i)
      }else{
	  
       if(IsPEBLFile(i))
        {
            tmppbl <- Append(tmppbl,i)
        
        }
     }
  }
  return [Sort(tmpdir),Sort(tmppbl)]

}



define DirToText(dirlist,filelist,offset,path)
{ 

  pathhead <- DirListTotext(path)

  tmp <- []
  loop(i,dirlist)
  {
    tmp <- Append(tmp,  i +"\"+ CR(1))
  }

  loop(i,filelist)
  {
    tmp <- Append(tmp,  i + CR(1))
  }

  ##this crashes here sometimes

  list <- SubList(tmp,offset+1,Length(tmp))

 return ListToString(list)
}

define AppendDirList(dirlist,dir)
{

 if(dir == ".")
   {
     dirlist <- dirlist
   }elseif(not dir == "..")   
   {
      dirlist <- Append(dirlist,dir)
   } else {
      if((not Last(dirlist) == ".") and
	     (not Last(dirlist) == ".."))
	  {
	      dirlist <- SubList(dirlist,1,Length(dirlist)-1)
      } else {
	      dirlist <- Append(dirlist,dir)
      }
   }

   return dirlist
}


##appends a set of nested directories into a path.
define DirlistToText(list)
{
  tmp <- ""
  loop(i,list)
  {		 
     tmp <- tmp + i+ "/"
  }
  return tmp
}


define  FileSaveTable(table,filename)
{

  outfile <- FileOpenWrite(gconfigName+".config")  
  loop(i,table)
    {
       FilePrint_(outfile, First(i) + ",")

	   if(IsList(Second(i)))
        {
		   sep <- ""
		   loop(j,Second(i))
           {
		      FilePrint_(outfile,sep+j)
			  sep <- ","
           }
		   FilePrint(outfile,"")
        } else {
		  FilePrint(outfile,Second(i))
        }
    }
  FileClose(outfile)
}


##This makes the text to put in the expchain box.
define MakeExpChainText(expchain)
{
	tmp <- ""
  loop(i,expchain)
	{
 	 tmp <- tmp +DirlistToText(First(i))+Second(i) + CR(1)
	  
    }
  return tmp	
}


##returns a list of 0/1 indicating which elements of 
##keys match key
define Match(key,keys)
{
  tmp <- []
  loop(i,keys)
  {
    tmp <- Append(tmp, key==i)
  }
 return tmp
}


define Filter(list,match)
{
  if(not Length(list)==Length(match))
  {
     SignalFatalError("List and match list much be of equal length in Filter(<list>,<match>)")
  }
  tmp <- []
  list2 <- Transpose([list,match])

  
  loop(i,list2)
  {

    if(Second(i))
    {
      tmp<- Append(tmp,First(i))
    }
  }
  return tmp
}


define RunScript(dirchain,filename,fullscreen,language,logfile,subcode,experimenter)
{

		 callstring <- "cd " + DirListToText(dirchain) + " ; " +gPEBLName + " "  +filename + " -s " + subcode
		 if(fullscreen)
		 {
		   callstring <- callstring + " --fullscreen "
                 }

		 callstring <- callstring + " --language " + language 
  
          if(GetSystemtype()=="LINUX")
           {
                callstring <- callstring + " > stdout.txt 2> stderr.txt"
           }

         FilePrint(logfile,TimeStamp() + "," + DirListToText(dirchain)+ filename+","+GetPEBLVersion() + "," + experimenter + "," + subcode + "," +"fullscreen:["+fullscreen+"],"+ language + "," + callstring+ ","+"STARTED")

         #### Run the experiment!!!!
         Print(callstring)
		 SystemCall(callstring,"")


         FilePrint(logfile,TimeStamp() + "," + DirListToText(dirchain)+filename+","+GetPEBLVersion() +
		 "," + experimenter + "," + subcode + "," +"fullscreen:["+fullscreen+"],"+ language + "," + callstring+ ","+"FINISHED")

}



define IsPEBLFile(fname)
{

  returnval <- 0
  len <- StringLength(fname)	
  if(len>4)	
           {
            if(SubString(fname,len-3,len)==".pbl")
	     			{
					  returnval <-  1
                    }

          }
   return returnval
}


define MakeButton(x,y,width,win,label)
{
   col <- MakeColor("grey70")
   button <-MakeCanvas(width,20,col)



   font<- MakeFont(gPEBLBaseFont,0,15,MakeColor("black"),MakeColor("white"),0)
   label <- MakeLabel(label,font)

   AddObject(label,button)
   Move(label,width/2,9)
   Draw(button)
   
   rect <-Rectangle(Floor(button.width/2),Floor(button.height/2),button.width,button.height,MakeColor("grey25"),0)
   AddObject(rect,button)
   Draw(rect)
   Move(rect,rect.x-1,rect.y-1)
   Draw(rect)
   Move(rect,rect.x-1,rect.y-1)
   Draw(rect)
#   Move(rect,rect.x-1,rect.y-1)
#   Draw(rect)
   RemoveObject(rect,button)

   AddObject(button,win)
   
   Move(button,x,y)
   
   return button
}


define PushButton(tmp)
{
	     Move(tmp,tmp.x+2,tmp.y+2)
		 Draw()
		 Wait(100)
		 Move(tmp,tmp.x-2,tmp.y-2)
		 Draw()
}

define ReadConfigFile(configname)
{

   if(FileExists(configName+".config"))
      {
       config <- (ReadCSV(gConfigName+".config"))
	   
	   ##read in the keys--first characters.
	   keys <- []
	   loop(i,config)
	   {
	     keys<- Append(keys,First(i))
       }

	   gfullscreen <- ToNumber(Second(Lookup("fullscreen",keys,config)))
	   gnimhdemographics <- ToNumber(Second(Lookup("nimhdemo",keys,config)))       
	   gsubcode    <- Second(Lookup("subcode",keys,config))
	   gexperimenter <-Second(Lookup("experimenter",keys,config))
	   glanguage <-Second(Lookup("language",keys,config))

   	   gexpchain <- []         ##sequence of experiments to run
   	   ##Get all config entries named 'expchain'
	   chains <- Filter(config,Match("EXPCHAIN",keys))
	   loop(i,chains)
        {
		   gexpchain <- Append(gexpchain,  [SubList(i,3,Length(i)),Second(i)])
        }  

      } else {

 
        gfullscreen <- 0
        gsubcode <- "1"
        gexperimenter <- "default"
        glanguage <- "en"
        gexpchain <- []         ##sequence of experiments to run
		gnimhdemographics <- 0
      }



}

define Saveconfig(configname)
{

	  ##save settings on exit.
	  out <- [["fullscreen",gfullscreen],
	          ["subcode",gsubcode],
			  ["experimenter",gexperimenter],
			  ["language",glanguage],
			  ["nimhdemo",gnimhdemographics]]

	  loop(i,gexpchain)
       {
	     ilist <- Merge([Second(i)],First(i))
	     out <- Append(out,["EXPCHAIN",ilist])
       }
      FileSaveTable(out,configname+".config")
}
